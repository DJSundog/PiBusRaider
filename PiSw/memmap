
MEMORY
{
    ram : ORIGIN = 0x8000, LENGTH = 512M
}

heap_size = 50M;
otaUpdateBufferSize = 10M;

SECTIONS
{
    .init : { *(.init) } > ram
    .text : { *(.text*) } > ram
    .rodata ALIGN(4096) : { *(.rodata*) } > ram

	.init_array ALIGN(4) : { 
        __init_array_start = .; 
        *(.init_array*)
        *(.ctors)
        __init_array_end = .; 
    } > ram

    .data ALIGN(4096): { *(.data*) } > ram

    /* Uninitialized data section */
    .bss ALIGN(4): {
        __bss_start__ = .;
        *(.bss*)
    } > ram 
    . = ALIGN(4);
    __bss_end__ = .;
    __bss_size__ = __bss_end__ - __bss_start__;

    .heap ALIGN(4): {
        _heap_start = .;
        . = . + heap_size;
        _heap_end = .;
    } > ram 

    .otaUpdateBuffer ALIGN(4): {
        _otaUpdateBufferStart = .;
        . = . + otaUpdateBufferSize;
        _otaUpdateBufferEnd = .;
    } > ram
    
    .ARM.attributes 0 : { *(.ARM.attributes) }

}

